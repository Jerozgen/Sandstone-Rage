@ require sr:global

# Инициализация игрока
if entity @s[tag=!init_player] function {
    gamemode adventure
    tp @s 0 200 0 -90 0
    spawnpoint @s 0 200 0
    tag @s add init_player
    team join none @s
    scoreboard players reset @s
    scoreboard players set @s changelog 1
    if score stage global matches 1 gamemode spectator
}

# Изменение цвета щита
for (team in teams) {
    store result entity @s[team=${team[0]}] Inventory[{id:"minecraft:shield"}].tag.BlockEntityTag.Base int 1 scoreboard players get ${(string) team[14]} const
}

# Ослабление топора
effect clear @s[
    x=17,y=200,z=13,distance=128..,
    nbt=!{SelectedItem:{id:"minecraft:wooden_axe"}},
    nbt=!{SelectedItem:{id:"minecraft:stone_axe"}},
    nbt=!{SelectedItem:{id:"minecraft:iron_axe"}},
    nbt=!{SelectedItem:{id:"minecraft:golden_axe"}},
    nbt=!{SelectedItem:{id:"minecraft:diamond_axe"}}
] weakness
effect give @s[nbt={SelectedItem:{id:"minecraft:wooden_axe"}}] weakness 99999 0 true
effect give @s[nbt={SelectedItem:{id:"minecraft:stone_axe"}}] weakness 99999 0 true
effect give @s[nbt={SelectedItem:{id:"minecraft:iron_axe"}}] weakness 99999 0 true
effect give @s[nbt={SelectedItem:{id:"minecraft:golden_axe"}}] weakness 99999 0 true
effect give @s[nbt={SelectedItem:{id:"minecraft:diamond_axe"}}] weakness 99999 0 true

# Файербол
store result score @s unluck effect clear @s unluck
if entity @s[scores={unluck=1..}] function {
    set x -> temp = @s.Pos[0] * 1000000
    set y -> temp = @s.Pos[1] * 1000000
    set z -> temp = @s.Pos[2] * 1000000
    
    summon area_effect_cloud ^ ^ ^0.001 {Tags:["fireball_init"],Duration:60}
    anchored eyes positioned ^ ^ ^ anchored feet function {
        summon area_effect_cloud ^ ^ ^0.8 {Tags:["fireball_spawner","spawner"],Duration:60}
        
        set @e[tag=fireball_spawner,tag=spawner,tag=!init,limit=1] -> dx = @e[tag=fireball_init,tag=!init,limit=1].Pos[0] * 1000000
        set @e[tag=fireball_spawner,tag=spawner,tag=!init,limit=1] -> dy = @e[tag=fireball_init,tag=!init,limit=1].Pos[1] * 1000000
        set @e[tag=fireball_spawner,tag=spawner,tag=!init,limit=1] -> dz = @e[tag=fireball_init,tag=!init,limit=1].Pos[2] * 1000000
        kill @e[tag=fireball_init]
        
        scoreboard players operation @e[tag=fireball_spawner,tag=spawner,tag=!init,limit=1] dx -= x temp
        scoreboard players operation @e[tag=fireball_spawner,tag=spawner,tag=!init,limit=1] dy -= y temp
        scoreboard players operation @e[tag=fireball_spawner,tag=spawner,tag=!init,limit=1] dz -= z temp
        tag @e[tag=fireball_spawner,tag=spawner] add init
        
        summon area_effect_cloud ^ ^ ^1.3 {Tags:["fireball_spawner","flame"],Duration:41}
        summon area_effect_cloud ^ ^ ^1.8 {Tags:["fireball_spawner","flame"],Duration:42}
        summon area_effect_cloud ^ ^ ^2.3 {Tags:["fireball_spawner","flame"],Duration:43}
        summon area_effect_cloud ^ ^ ^2.8 {Tags:["fireball_spawner","flame"],Duration:44}
        summon area_effect_cloud ^ ^ ^3.3 {Tags:["fireball_spawner","flame"],Duration:45}
        summon area_effect_cloud ^ ^ ^3.8 {Tags:["fireball_spawner","flame"],Duration:46}
        summon area_effect_cloud ^ ^ ^4.3 {Tags:["fireball_spawner","flame"],Duration:47}
        summon area_effect_cloud ^ ^ ^4.8 {Tags:["fireball_spawner","flame"],Duration:48}
        summon area_effect_cloud ^ ^ ^5.3 {Tags:["fireball_spawner","flame"],Duration:49}
        summon area_effect_cloud ^ ^ ^5.8 {Tags:["fireball_spawner","flame"],Duration:50}
    }
}


# Автоплавка
for (el in ["iron_ingot", "gold_ingot"]) {
    if score @s ${el} matches 1.. function {
        function give {
            /${"give @s " + el}
            xp add @s 1 points
        }
        if entity @s[nbt={SelectedItem:{tag:{Enchantments:[{id:"minecraft:fortune",lvl:2s}]}}}] function /give
        if entity @s[nbt={SelectedItem:{tag:{Enchantments:[{id:"minecraft:fortune",lvl:3s}]}}}] function /give
        
        scoreboard players reset @s $el
    }
}
# Антизастревание в блоке-конструкторе
if entity @s[gamemode=!spectator] if block ~ ~ ~ structure_block align y tp ~ ~1 ~


# Остановка наблюдателей
if score @s spec_stun matches 1.. function {
    if entity @s[gamemode=spectator] function {
        if score minutes global matches ..154 tp @s 908 1000 1018
        if score minutes global matches 155.. tp @s -1000 1000 0
        gamemode adventure
    }
    if score @s spec_stun matches 1 if entity @s[gamemode=adventure] gamemode spectator
    scoreboard players remove @s spec_stun 1
}


# Возврат командных монет
for (team in teams) {
    if entity @s[team=!${team[0]}] store result score ${team[0]+"_return"} temp clear @s cod_spawn_egg{Tags:[${new nbt(team[0]+"_coin")}]}
    if score ${team[0]+"_return"} temp matches 1.. function {
        summon item ~ ~ ~ {PickupDelay:20s,Tags:["countme"],Item:{id:"minecraft:cod_spawn_egg",Count:1b,tag:{Tags:[${new nbt(team[0] + "_coin")},"return"],
                                EntityTag:{id:"minecraft:armor_stand",NoGravity:1b,Marker:1b,Invisible:1b,Silent:1b,Tags:["bag"]},display:{
                                    Name:${new nbt(new text_component({italic:false,color:"gold",text:"Мешок монет " + team[4] + " команды"}))},
                                    Lore:["§r§7Используйте, чтобы превратить в 6 монет"]
                                }}}}
        as @e[type=item,tag=countme] function {
            store result entity @s Item.Count byte 1.0 scoreboard players get ${team[0]+"_return"} temp
            tag @s remove countme
        } 
        scoreboard players reset ${team[0]+"_return"} temp
    }
}

# Гиганский пузырёк опыта
if entity @s[scores={use.xpbottle=1..}] function {
    xp add @s 612 points
    kill @e[type=experience_bottle]
    scoreboard players reset @s use.xpbottle
}


# Удочка с морковью
if entity @s[scores={use.carrot_stick=1..}] function {
    unless entity @s[x=17,y=200,z=13,distance=..128] unless entity @s[x=-1000,y=100,z=0,distance=..256] function {
        unless block ~ ~-1 ~ #sr:do_not_replace_by_slime function {
            unless entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick"}}] unless entity @s[nbt={Inventory:[{Slot:-106b,id:"minecraft:carrot_on_a_stick"}]}] clear @s carrot_on_a_stick 1
            unless entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick"}}] if entity @s[nbt={Inventory:[{Slot:-106b,id:"minecraft:carrot_on_a_stick"}]}] replaceitem entity @s weapon.offhand air
            if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick"}}] replaceitem entity @s weapon.mainhand air
            setblock ~ ~-1 ~ slime_block destroy
        }
    }
    scoreboard players reset @s use.carrot_stick
}


# Элитры
if entity @s[nbt={Inventory:[{Slot:102b,id:"minecraft:elytra"}]}] function {
    if score @s aviate matches 1.. function {
        at @s particle campfire_cosy_smoke ~ ~ ~ 0 0 0 0 1 force
        if entity @s[x=-3,y=203,z=0,distance=..5] function {
            for (team in teams) {
                raw ${"if entity @s" + team[8].replace("nbt={OnGround:1b},","")} function {
                    eval log(entity<@a[team=${team[0]}]>,[{selector:"@s"},{text:" покинул ожидание принятия в команду."}])
                    eval log(entity<@s>,"Вы покинули ожидание принятия в команду.")
                }
            }
            tp @s 0 200 0
            scoreboard players reset @s leavewaiting
        }
        scoreboard players reset @s aviate
    }
    if entity @s[tag=elytra_taken_damage] function {
        kill @s
    }
}
if entity @s[tag=elytra_taken_damage] function {
    advancement revoke @s only sr:_elytra_taken_damage
    tag @s remove elytra_taken_damage
}


# Уменьшение времени голода с кадавра
if entity @s[tag=hunger_fix] function {
    effect clear @s hunger
    effect give @s hunger 8
    tag @s remove hunger_fix
    advancement revoke @s only sr:_hunger_fix
}

# Удаление эффекта со спектральной стрелы
if entity @s[tag=glowing_fix] function {
    effect clear @s glowing
    tag @s remove glowing_fix
    advancement revoke @s only sr:_glowing_fix
}

# Триггеры
scoreboard players enable @s changelog
scoreboard players enable @s help
scoreboard players enable @s authors
scoreboard players enable @s testers
unless score @s changelog matches 0 function sr:changelog
unless score @s help matches 0 function sr:help
unless score @s authors matches 0 function {
    tellraw @s ["",{"text":"   ------ [Sandstone "},{"text":"Rage","bold":true,"color":"gold"},{"text":"] ------\n\n"},{"text":"     \u041a\u043e\u0434:","color":"gold"},{"text":" Jerozgen\n"},{"text":"     \u041f\u043e\u0441\u0442\u0440\u043e\u0439\u043a\u0438:","color":"gold"},{"text":" Kon4ita\n\n"},{"text":"     \u0421\u043e\u0432\u0435\u0442\u044b:","color":"gold"},{"text":" CorbinADtor\n"},{"text":"     \u041f\u043e\u043c\u043e\u0449\u044c \u0432 \u043f\u043e\u0441\u0442\u0440\u043e\u0439\u043aах:","color":"gold"},{"text":" \n       Sancho_tEm\n       StuART_Original\n"},{"text":"     Ранний код:","color":"gold"},{"text":" GreenL1nk\n"},{"text":"     Ранние постройки:","color":"gold"},{"text":" \n       luka4er\n       VilkinOff\n"},{"text":"     \u0422\u0435\u0441\u0442\u0435\u0440\u044b:","color":"gold"},{"text":"\n       "},{"text":"[\u041f\u043e\u0441\u043c\u043e\u0442\u0440\u0435\u0442\u044c \u0441\u043f\u0438\u0441\u043e\u043a]","color":"green","clickEvent":{"action":"run_command","value":"/trigger testers"}},{"text":"\n\n   ----------------------------"}]
    set @s -> authors = 0
}
unless score @s testers matches 0 function {
    tellraw @s ["",{"text":"   ------ [Sandstone "},{"text":"Rage","bold":true,"color":"gold"},{"text":"] ------\n\n"},{"text":"     \u0422\u0435\u0441\u0442\u0435\u0440\u044b:","color":"gold"},{"text":" \n       12kravasanch1k12\n       __Tord_Larsson__\n       _Bloody_1337\n       _FireFiro_\n       _Timchik_\n       _Xeks_\n       _Zip_Bro_\n       "},{"text":"A","color":"dark_aqua"},{"text":"DequateZ\n       AkenoSenxpi_\n       Alexsandriya\n       AliciaLP\n       AnzhelinaYT\n       "},{"text":"B","color":"dark_aqua"},{"text":"1rdynyannyan\n       BlackStar_YT\n       BoomFoxx\n       BrandShei\n       "},{"text":"c","color":"dark_aqua"},{"text":"atastrophe122\n       "},{"text":"D","color":"dark_aqua"},{"text":"evastational\n       DiaLight\n       Dim0N\n       DJkiller\n       DobruyChelove4ek\n       DSempai\n       "},{"text":"E","color":"dark_aqua"},{"text":"l7\n       Epic_Addy\n       ewoka20\n       "},{"text":"F","color":"dark_aqua"},{"text":"ake_Zomb1e\n       Fedoras_\n       Fiarcherry\n       Freddyt98\n       "},{"text":"G","color":"dark_aqua"},{"text":"oldenGuppies\n       Grim0shi\n       "},{"text":"H","color":"dark_aqua"},{"text":"1kka\n       Hemok98\n       Husplace\n       "},{"text":"I","color":"dark_aqua"},{"text":"amPebble\n       IamWorldPad\n       ItsRosey\n       iUnlegit\n       "},{"text":"j","color":"dark_aqua"},{"text":"bcurtis\n       JeeANiiT\n       JekaRUS\n       JorniKek\n       JustEmpty05\n       "},{"text":"K","color":"dark_aqua"},{"text":"azuhira_Fogg\n       KennyDC\n       Ksant\n       "},{"text":"L","color":"dark_aqua"},{"text":"oBoKryaK\n       Lullolia\n       "},{"text":"M","color":"dark_aqua"},{"text":"aksimyss\n       Makso4iks\n       ManuelGo2\n       MarcMc\n       Maxx0520\n       megabalker\n       MHGjj4win\n       MrDingos\n       MrHixuu\n       MrLegolas\n       "},{"text":"n","color":"dark_aqua"},{"text":"annis123\n       Nastya_VeterokYT\n       Neaton_01\n       Nicralet52RUS\n       NlKO45\n       no_op_guy\n       "},{"text":"O","color":"dark_aqua"},{"text":"nurAlpaslan1\n       "},{"text":"P","color":"dark_aqua"},{"text":"ersonalfebus\n       PondikPanda\n       "},{"text":"Q","color":"dark_aqua"},{"text":"BER_0\n       Qwerty_GG\n       "},{"text":"r","color":"dark_aqua"},{"text":"odabdel\n       rutek283\n       "},{"text":"S","color":"dark_aqua"},{"text":"irboys\n       SnigleN\n       "},{"text":"T","color":"dark_aqua"},{"text":"eapal\n       The_Noobscoper\n       Thonic\n       "},{"text":"V","color":"dark_aqua"},{"text":"adem08\n       varga1989\n       "},{"text":"x","color":"dark_aqua"},{"text":"KILADx\n\n   ----------------------------"}]
    set @s -> testers = 0
}

scoreboard players add @s spectate 0
unless score @s spectate matches 0 function {
    if entity @s[team=none] function {
        gamemode spectator @s
        tp @s 908 112 1018
    }
    
    set @s -> spectate = 0
}
