@ require sr:global
@ require sr:stages/game/player
@ require sr:shops

# Точки возрождения
spawnpoint @a[team=aqua] 1040 82 1018
spawnpoint @a[team=yellow] 908 82 886
spawnpoint @a[team=green] 776 82 1018
spawnpoint @a[team=purple] 908 82 1150


# Достижения
unless score adv.candle global matches 1.. unless block 908 82 1018 torch function {
    advancement grant @a[scores={adv.mine_torch=1..}] only sr:candle
    scoreboard players set adv.candle global 1
}
scoreboard players reset @a adv.mine_torch


# Предотвращение превращения крестьян в ведьм
as @e[type=witch] function {
    function sr:shops/spawn
    kill @s
}


# Командный мешок монет
as @e[type=item,nbt={Item:{id:"minecraft:cod_spawn_egg"}}] data merge entity @s {Invulnerable:1b}


# Предотвращение попадания визера в бездну
as @e[type=wither] at @s if entity @s[y=32,dy=-100] data modify entity @s Motion[1] set value 0.5


# Ядра
for (team in teams) {
    at @e[tag=${"nexus_" + team[0]}] unless block ~ ~ ~ diamond_ore if score ${team[1]} Nexus matches 1.. function {
        setblock ~ ~ ~ diamond_ore
        kill @e[type=item,distance=..0.5]
        kill @e[type=experience_orb,distance=..0.5]
        as @a[scores={mine_diamond_ore=1..},distance=..10] function {
            if entity @s[team=${team[0]}] function {
                scoreboard players remove @s adv.mine_diamond 1
                title @s[team=${team[0]}] actionbar {"text":"Нельзя разрушать своё ядро!","color":"red"}
            }
            unless entity @s[team=${team[0]}] function {
                var give_diamonds = resource</give_diamonds>
                function give_diamonds {
                    give @s diamond
                    xp add @s 5 points
                }
                if entity @s[nbt={SelectedItem:{tag:{Enchantments:[{id:"minecraft:fortune",lvl:2s}]}}}] function $give_diamonds
                if entity @s[nbt={SelectedItem:{tag:{Enchantments:[{id:"minecraft:fortune",lvl:3s}]}}}] function {
                    function $give_diamonds
                    function $give_diamonds
                }
                scoreboard players remove ${team[1]} Nexus 1
                as @a[team=${team[0]}] at @s playsound item.shield.block block @s ~ ~ ~ 1 0
                playsound entity.elder_guardian.hurt block @a ~ ~ ~ 1 0
                title @a[team=${team[0]}] actionbar {"text":"Ваше ядро разрушают!","color":"red"}
                unless score ${team[0]+"_notification"} global matches 1..
                    as @a[team=!${team[0]}] at @s playsound block.note_block.pling player @s
                set ${(entity) (team[0]+"_notification")} -> global = 50
                
                if score ${team[1]} Nexus matches 0 function {
                    playsound entity.zombie_villager.cure block @a ~ ~ ~ 25 0.9
                    playsound entity.lightning_bolt.thunder ambient @a ~ ~ ~ 25 0.7
                    playsound item.totem.use player @a ~ ~ ~ 25 0
                    setblock ~ ~ ~ structure_block
                    
                    bossbar set ${(resource) team[0]} name [{"text":"Ядро ","color":"white"},{"text":${team[5]},"color":${team[2]}},{"text":" разрушено!","color":"white"}]
                    bossbar set ${(resource) team[0]} color red
                    bossbar set ${(resource) team[0]} max 100
                    set ${(entity) (team[0]+"_notification")} -> global = 100
                    eval log(entity<@a>, ["Ядро команды ",{text:team[5],color:team[2]}," разрушено."])
                    eval log(entity<@a[team=${team[0]}]>, ["За потерю ядра команда получила по 6 монет на каждого участника и скорость."])
                    
                    give @a[team=${team[0]},gamemode=!spectator,scores={time_since_death=1..}] cod_spawn_egg{EntityTag:{id:"minecraft:armor_stand",NoGravity:1b,Marker:1b,Invisible:1b,Silent:1b,Tags:["bag"]},
                        display:{
                            Name:${new nbt(new text_component({italic:false,color:"gold",text:"Мешок монет " + team[4] + " команды"}))},
                            Lore:['"§r§7Используйте, чтобы превратить в 6 монет"']},Tags:[${new nbt(team[0] + "_coin")},"return"]
                        }
                    as @a[team=${team[0]},gamemode=!spectator,scores={time_since_death=0}] 
                        summon item{Item:{id:"minecraft:cod_spawn_egg",Count:1b,tag:{Tags:[${new nbt(team[0] + "_coin")},"return"],
                                                EntityTag:{id:"minecraft:armor_stand",NoGravity:1b,Marker:1b,Invisible:1b,Silent:1b,Tags:["bag"]},display:{
                                                    Name:${new nbt(new text_component({italic:false,color:"gold",text:"Мешок монет " + team[4] + " команды"}))},
                                                    Lore:['"§r§7Используйте, чтобы превратить в 6 монет"']
                                                }}}} ${team[6]}
                }
            }
        }
    }   
}
scoreboard players reset @a mine_diamond_ore

# Оповещения о разрушении ядра
for (team in teams) {
    if score ${team[0]+"_notification"} global matches 1.. function {
        store result bossbar ${(resource) team[0]} value scoreboard players get ${team[0]+"_notification"} global
        bossbar set ${(resource) team[0]} players @a
        scoreboard players remove ${team[0]+"_notification"} global 1
        if score ${team[0]+"_notification"} global matches 0
            bossbar set ${(resource) team[0]} players
    }
}


as @a at @s function sr:stages/game/player


# Сущность «Покинуть наблюдение»
as @e[tag=spectator_exit] at @s tp @s ~ ~ ~ ~1 ~


# Отображение информации о серии убийств
bossbar set kill_streak players @a[scores={streak_bossbar=1..}]
scoreboard players remove @a[scores={streak_bossbar=1..}] streak_bossbar 1


# Голем
as @e[type=iron_golem] at @s function {
    if score @s angry_timer matches 1.. function {
        scoreboard players remove @s angry_timer 1
        if score @s angry_timer matches 0 function {
            set @s.ArmorItems[3].tag.AttackUUIDLeast = 0
            set @s.ArmorItems[3].tag.AttackUUIDMost = 0
        }
    }
    for (team in teams) {
        if entity @s[team=${team[0]}] if entity @a[distance=..8,team=!${team[0]},team=!none,gamemode=!spectator,gamemode=!creative,nbt=!{ActiveEffects:[{Id:14b}]}] function {
            store result score compare temp 
                set @s.ArmorItems[3].tag.AttackUUIDLeast = @p[team=!${team[0]},team=!none,gamemode=!spectator,gamemode=!creative,nbt=!{ActiveEffects:[{Id:14b}]}].UUIDLeast
            unless score compare temp matches 0 function {
                store result score compare temp 
                    set @s.ArmorItems[3].tag.AttackUUIDMost = @p[team=!${team[0]},team=!none,gamemode=!spectator,gamemode=!creative,nbt=!{ActiveEffects:[{Id:14b}]}].UUIDMost
                unless score compare temp matches 0 function {
                    set @s -> angry_timer = 200
                    summon area_effect_cloud{
                        Tags: ["angry"], 
                        Duration: 10, DurationOnUse: 0, WaitTime: 0, Particle: "block air", 
                        Effects:[{Id: 7b, Duration: 10, Amplifier: 31b, ShowParticles:0b}], 
                        Radius: 1.0f, RadiusOnUse: 0.0f, RadiusPerTick: 0.0f}
                    set @e[tag=angry,sort=nearest,limit=1].OwnerUUIDLeast = @p[team=!aqua,gamemode=!spectator].UUIDLeast
                    set @e[tag=angry,sort=nearest,limit=1].OwnerUUIDMost = @p[team=!aqua,gamemode=!spectator].UUIDMost
                }
            }
        }
    }
}

# Тёмный оберег
as @e[type=item,nbt={Item:{id:"minecraft:music_disc_stal"}}] function {
    set @s.Invulnerable = 1b
    set @s.Age = 1s
    at @s particle smoke ~ ~.35 ~ 0 0 0 0.02 3 force
    data merge entity @s {NoGravity:1b}
}


# Время и события
unless score disablewin global matches 1 function {
    scoreboard players add timer global 1
    if score timer global matches 1200.. function {
        scoreboard players set timer global 0
        scoreboard players add minutes global 1
        var i = 0
        for(i; i < 300; i += 1) {
            var h = i / 60
            var m = i % 60
            do if (m < 10) eval m = "0" + m
            if score minutes global matches $i team modify _time suffix [": ",{"text":${(string) h},"color":"gold"},":",{"text":${(string) m},"color":"gold"}]
        }
        # 0:01
        if score minutes global matches 1 function {
            for (team in teams) {
                unless score ${team[1]} Nexus matches 1.. at @e[tag=${"nexus_" + team[0]}] setblock ~ ~ ~ structure_block
            }
        }
        # 0:10
        if score minutes global matches 10 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Через ",{"text":"5 минут","color":"red"}," (в 0:15) в центральном магазине появится ",{"text":"[тёмный оберег]","color":"aqua","hoverEvent":{"action":"show_item","value":"{id:music_disc_stal,Count:1b,tag:{HideFlags:63,Enchantments:[{id:\"fortune\",lvl:-1s}],display:{Name:'{\"text\":\"Тёмный оберег\",\"color\":\"aqua\",\"italic\":false}',Lore:['\"\"','\"§9Владелец§7 при попадании в бездну\"','\"§eвозвращается на базу§7, восстана-\"','\"§7вливая здоровье и сытость, если\"','\"§7ядро не разрушено.\"','\"\"','\"§7Неуязвим.\"','\"§7Не подвержен гравитации.\"']}}}"}}," в ",{"text":"единственном","underlined":true}," экземпляре."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
        }
        # 0:15
        if score minutes global matches 15 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["В центральном магазине появился ",{"text":"[тёмный оберег]","color":"aqua","hoverEvent":{"action":"show_item","value":"{id:music_disc_stal,Count:1b,tag:{HideFlags:63,Enchantments:[{id:\"fortune\",lvl:-1s}],display:{Name:'{\"text\":\"Тёмный оберег\",\"color\":\"aqua\",\"italic\":false}',Lore:['\"\"','\"§9Владелец§7 при попадании в бездну\"','\"§eвозвращается на базу§7, восстана-\"','\"§7вливая здоровье и сытость, если\"','\"§7ядро не разрушено.\"','\"\"','\"§7Неуязвим.\"','\"§7Не подвержен гравитации.\"']}}}"}}," в ",{"text":"единственном","underlined":true}," экземпляре."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            positioned 908 92 1018 as @e[limit=1,tag=shop.center,distance=..1] function {
                data modify entity @s Offers.Recipes[7].maxUses set value 1
                data remove entity @s Offers.Recipes[7].sell.tag.display.Lore[0]
            }
            playsound ambient.cave master @a 908 92 1018 100 0.7
        }
        # 0:40
        if score minutes global matches 35 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Через ",{"text":"10 минут","color":"red"}," (в 0:45) в центральном магазине появится второй ",{"text":"[тёмный оберег]","color":"aqua","hoverEvent":{"action":"show_item","value":"{id:music_disc_stal,Count:1b,tag:{HideFlags:63,Enchantments:[{id:\"fortune\",lvl:-1s}],display:{Name:'{\"text\":\"Тёмный оберег\",\"color\":\"aqua\",\"italic\":false}',Lore:['\"\"','\"§9Владелец§7 при попадании в бездну\"','\"§eвозвращается на базу§7, восстана-\"','\"§7вливая здоровье и сытость, если\"','\"§7ядро не разрушено.\"','\"\"','\"§7Неуязвим.\"','\"§7Не подвержен гравитации.\"']}}}"}}," в ",{"text":"единственном","underlined":true}," экземпляре."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
        }
        # 0:45
        if score minutes global matches 45 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["В центральном магазине появился второй ",{"text":"[тёмный оберег]","color":"aqua","hoverEvent":{"action":"show_item","value":"{id:music_disc_stal,Count:1b,tag:{HideFlags:63,Enchantments:[{id:\"fortune\",lvl:-1s}],display:{Name:'{\"text\":\"Тёмный оберег\",\"color\":\"aqua\",\"italic\":false}',Lore:['\"\"','\"§9Владелец§7 при попадании в бездну\"','\"§eвозвращается на базу§7, восстана-\"','\"§7вливая здоровье и сытость, если\"','\"§7ядро не разрушено.\"','\"\"','\"§7Неуязвим.\"','\"§7Не подвержен гравитации.\"']}}}"}}," в ",{"text":"единственном","underlined":true}," экземпляре."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            positioned 908 92 1018 as @e[limit=1,tag=shop.center,distance=..1] function {
                data modify entity @s Offers.Recipes[8].maxUses set value 1
                data remove entity @s Offers.Recipes[8].sell.tag.display.Lore[0]
            }
            playsound ambient.cave master @a 908 92 1018 100 0.7
        }
        # 1:15
        if score minutes global matches 75 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Через ",{"text":"10 минут","color":"red"}," (в 1:25) ядра потеряют 50% ОЗ."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
        }
        # 1:25
        if score minutes global matches 85 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["",{"text":"Прошло "},{"text":"1:25","color":"gold"}," с начала игры. Все ядра потеряли 50% ОЗ."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            for (team in teams) {
                if score ${team[1]} Nexus matches 1.. function {
                    scoreboard players operation diamonds temp = ${team[1]} Nexus
                    scoreboard players operation diamonds temp /= 2 global
                    scoreboard players operation ${team[1]} Nexus -= diamonds temp
                    #if score diamonds temp matches 1.. function {
                    #    summon item{NoGravity:1b,Invulnerable:1b,PickupDelay:20s,Tags:["countme"],Item:{id:"minecraft:diamond",Count:1b}} ${team[6]}
                    #    as @e[type=item,tag=countme] function {
                    #        store result entity @s Item.Count byte 1.0 scoreboard players get diamonds temp
                    #        tag @s remove countme
                    #    }
                    #}
                }
            }
        }
        # 1:35
        if score minutes global matches 95 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Через ",{"text":"10 минут","color":"red"}," (в 1:45) ядра будут перенесены на остров железа."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
        }
        # 1:45
        if score minutes global matches 105 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["",{"text":"Прошло "},{"text":"1:45","color":"gold"},{"text":" с начала игры. Ядра "},{"text":"перенесены","color":"red"}," на остров железа."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            at @e[tag=nexus] setblock ~ ~ ~ structure_block
            for (team in teams) {
                tp @e[tag=${"nexus_" + team[0]}] ${team[12]}
                if score ${team[1]} Nexus matches 1.. at @e[tag=${"nexus_" + team[0]}] setblock ~ ~ ~ diamond_ore
            }
        }
        # 2:05
        if score minutes global matches 125 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Через ",{"text":"10 минут","color":"red"}," (в 2:15) граница мира начнёт двигаться к центру."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
        }
        # 2:15
        if score minutes global matches 135 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["",{"text":"Прошло "},{"text":"2:15","color":"gold"},{"text":" с начала игры. "},{"text":"Граница мира","color":"yellow"},{"text":" начала движение к центру. Через "},{"text":"5 минут","color":"red"},{"text":" будет отключено возрождение игроков."}])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            worldborder set 300
            worldborder set 265 300
        }
        # 2:20
        if score minutes global matches 140 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Возрождение игроков отключено."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            for (team in teams) {
                team modify ${team[9]} prefix ["",{"text":"×","color":${team[2]}}," "]
            }
            worldborder set 265
            worldborder set 5 600
        }
        # 2:30
        if score minutes global matches 150 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Через ",{"text":"5 минут","color":"red"}," (в 2:35) произойдёт телепортация игроков."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
        }
        # 2:35
        if score minutes global matches 155 function {
            title @a times 3 7 3
            title @a title {"text":"!","bold":true,"color":"red"}
            eval log(entity<@a>, ["Телепортация."])
            as @a at @s playsound entity.arrow.hit_player master @s ~ ~ ~ 1 0.85
            #worldborder set 7
            #fill 904 255 1022 912 255 1014 air replace structure_void
            #/spreadplayers 908 1018 6 3 true @a[gamemode=survival]
            #worldborder set 5 10
            time set midnight
            worldborder center -1000 0
            worldborder set 59
            worldborder warning distance 200
            gamerule mobGriefing false
            kill @e[type=ender_pearl]
            tp @a[team=aqua] -973 80 0 90 0
            tp @a[team=purple] -1000 80 27 180 0
            tp @a[team=green] -1027 80 0 -90 0
            tp @a[team=yellow] -1000 80 -27 0 0
            tp @a[team=none] -1000 100 0
            gamemode adventure @a[gamemode=!spectator]
            clear @a[gamemode=adventure] carrot_on_a_stick
        }
    }
}

unless score Голубые Nexus matches 1.. effect give @a[gamemode=!spectator,team=aqua,x=900,y=82,z=1018,distance=..250] speed 99999 0 true
unless score Зелёные Nexus matches 1.. effect give @a[gamemode=!spectator,team=green,x=900,y=82,z=1018,distance=..250] speed 99999 0 true
unless score Фиолетовые Nexus matches 1.. effect give @a[gamemode=!spectator,team=purple,x=900,y=82,z=1018,distance=..250] speed 99999 0 true
unless score Жёлтые Nexus matches 1.. effect give @a[gamemode=!spectator,team=yellow,x=900,y=82,z=1018,distance=..250] speed 99999 0 true
    
if score minutes global matches 135.. gamemode spectator @a[x=17,y=200,z=13,distance=..128]



/execute store result score players global if entity @a[team=!none]
set sub_players -> temp = pre_players -> global
scoreboard players operation sub_players temp -= players global
if score sub_players temp matches 2.. function {
    set disablewin -> global = 1
    eval log(entity<@a>, ["",{"text":"Победы отключены. Чтобы включить, введите: "},{"text":"[/function sr:win_enable]","color":"green","clickEvent":{"action":"run_command","value":"/function sr:win_enable"}}])
}

unless score disablewin global matches 1 function {
    # Победа
    if entity @a[gamemode=!spectator,team=green] 
        unless entity @a[gamemode=!spectator,team=yellow] 
        unless entity @a[gamemode=!spectator,team=aqua] 
        unless entity @a[gamemode=!spectator,team=purple] 
            function {
                function sr:gend
                eval log(entity<@a>, ["Победила ",{"text":"зелёная","color":"green"}," команда!"])
            }
    unless entity @a[gamemode=!spectator,team=green] 
        if entity @a[gamemode=!spectator,team=yellow] 
        unless entity @a[gamemode=!spectator,team=aqua] 
        unless entity @a[gamemode=!spectator,team=purple] 
            function {
                function sr:gend
                eval log(entity<@a>, ["Победила ",{"text":"жёлтая","color":"yellow"}," команда!"])
            }
    unless entity @a[gamemode=!spectator,team=green] 
        unless entity @a[gamemode=!spectator,team=yellow] 
        if entity @a[gamemode=!spectator,team=aqua] 
        unless entity @a[gamemode=!spectator,team=purple] 
            function {
                function sr:gend
                eval log(entity<@a>, ["Победила ",{"text":"голубая","color":"aqua"}," команда!"])
            }
    unless entity @a[gamemode=!spectator,team=green] 
        unless entity @a[gamemode=!spectator,team=yellow] 
        unless entity @a[gamemode=!spectator,team=aqua] 
        if entity @a[gamemode=!spectator,team=purple] 
            function {
                function sr:gend
                eval log(entity<@a>, ["Победила ",{"text":"фиолетовая","color":"dark_purple"}," команда!"])
            }
}
if score disablewin global matches 1 title @a actionbar ["Победы отключены! ",{"text":"/function sr:win_enable","color":"gold"}]
set pre_players -> global = players -> global

define function sr:win_enable {
    scoreboard players reset disablewin global
}
define function sr:win_disable {
    set disablewin -> global = 1
}

function sr:antibuild
